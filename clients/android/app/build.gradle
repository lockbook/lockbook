apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'org.jmailen.kotlinter'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "app.lockbook"
        minSdkVersion 22
        targetSdkVersion 32
        versionCode 22
        versionName "0.5.11"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            ext.maybeStoreFile = System.getenv("ANDROID_RELEASE_STORE_FILE")

            if(maybeStoreFile != null) {
                storeFile file(maybeStoreFile)
                storePassword System.getenv("ANDROID_RELEASE_STORE_PASSWORD")
                keyAlias System.getenv("ANDROID_RELEASE_KEY_ALIAS")
                keyPassword System.getenv("ANDROID_RELEASE_KEY_PASSWORD")
            }
        }
    }

    lintOptions {
        warningsAsErrors true
        htmlReport false
        xmlReport false
        abortOnError true
        lintConfig file('../lint.xml')
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled false
	        zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    kotlinter {
        disabledRules = ["no-wildcard-imports", "parameter-list-wrapping"]
    }

    testOptions {
        unitTests.all {
            jvmArgs '-Djava.library.path=../core/src/main/jniLibs/desktop'
        }
    }

    android.buildFeatures.viewBinding = true
}

task printVersionCode {
    doLast {
        print android.defaultConfig.versionCode
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Ui Libraries
    implementation "com.google.android.material:material:$material_ui_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.slidingpanelayout:slidingpanelayout:$sliding_pane_layout_version"
    implementation "com.afollestad:recyclical:$recyclical_version"
    implementation "com.github.MikeOrtiz:TouchImageView:$image_viewer_version"
    implementation "com.github.mhiew:android-pdf-viewer:$pdf_viewer_version"
    implementation "com.leinardi.android:speed-dial:$fab_speed_dial"
    implementation("app.futured.donut:donut:$donut_chart_version")
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.biometric:biometric:$fingerprint_version"
    implementation "com.journeyapps:zxing-android-embedded:$qr_embedded_version"

    // Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$json_parser_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_reflect"


    // Android KTX
    implementation "androidx.core:core-ktx:$core_ktx_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$refresh_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_android_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    // Markdown
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:editor:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation "io.noties.markwon:ext-latex:$markwon_version"
    implementation "io.noties.markwon:image:$markwon_version"

    // Testing Libraries
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$junit_test_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_test_version"
    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "androidx.test:rules:$test_runner_version"

    // Miscellaneous
    implementation "com.michael-bull.kotlin-result:kotlin-result:$result_version"
    implementation "com.jakewharton.timber:timber:$logger_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "com.android.billingclient:billing-ktx:$billing_version"

    implementation project(':core')
}
